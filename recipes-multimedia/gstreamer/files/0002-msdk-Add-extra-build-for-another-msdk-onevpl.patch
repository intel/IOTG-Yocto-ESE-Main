From dd0529985e923764bec0e7c92d157b270e59500e Mon Sep 17 00:00:00 2001
From: "Ung, Teng En" <teng.en.ung@intel.com>
Date: Wed, 4 Aug 2021 14:58:49 +0000
Subject: [PATCH 2/2] msdk: Add extra build for another msdk onevpl.

---
 sys/msdk/meson.build        |   5 ++
 sys/msdk/onevpl/meson.build | 115 ++++++++++++++++++++++++++++++++++++
 2 files changed, 120 insertions(+)
 create mode 100644 sys/msdk/onevpl/meson.build

diff --git a/sys/msdk/meson.build b/sys/msdk/meson.build
index 84dc58f54..3f43bf594 100644
--- a/sys/msdk/meson.build
+++ b/sys/msdk/meson.build
@@ -179,3 +179,8 @@ if msdk_deps_found
   plugins += [gstmsdktag]
   have_msdk = true
 endif
+
+# Add another build just for GST-MSDK of onevpl if default build GST-MSKD of MSDK.
+if have_msdk and not use_onevpl
+  subdir('onevpl')
+endif
diff --git a/sys/msdk/onevpl/meson.build b/sys/msdk/onevpl/meson.build
new file mode 100644
index 000000000..02ff9224b
--- /dev/null
+++ b/sys/msdk/onevpl/meson.build
@@ -0,0 +1,115 @@
+msdk_ovpl_sources = [
+  '../gstmsdk.c',
+  '../gstmsdkbufferpool.c',
+  '../gstmsdkcontext.c',
+  '../gstmsdkcontextutil.c',
+  '../gstmsdkdec.c',
+  '../gstmsdkdecproputil.c',
+  '../gstmsdkenc.c',
+  '../gstmsdkh264dec.c',
+  '../gstmsdkh264enc.c',
+  '../gstmsdkh265dec.c',
+  '../gstmsdkh265enc.c',
+  '../gstmsdkmjpegdec.c',
+  '../gstmsdkmjpegenc.c',
+  '../gstmsdkmpeg2dec.c',
+  '../gstmsdkmpeg2enc.c',
+  '../gstmsdksystemmemory.c',
+  '../gstmsdkvc1dec.c',
+  '../gstmsdkvideomemory.c',
+  '../gstmsdkvp8dec.c',
+  '../gstmsdkvpp.c',
+  '../gstmsdkvpputil.c',
+  '../msdk-enums.c',
+  '../msdk.c',
+]
+
+msdk_ovpl_extra_args = []
+
+if host_machine.system() == 'windows'
+  msdk_ovpl_sources += ['../msdk_d3d.c', '../gstmsdkallocator_d3d.c' ]
+else
+  msdk_ovpl_sources += ['../msdk_libva.c', '../gstmsdkallocator_libva.c']
+endif
+
+msdk_dep = []
+
+msdk_option = get_option('msdk')
+if msdk_option.disabled()
+  subdir_done()
+endif
+
+mfx_ovpl_dep = dependency('vpl', version: '>= 2.2', required: false)
+
+if mfx_ovpl_dep.found()
+  mfx_ovpl_incdir = mfx_ovpl_dep.get_pkgconfig_variable('includedir')
+  mfx_ovpl_inc = []
+else
+  message('OneVPL SDK not found: consider setting PKG_CONFIG_PATH, INTELMEDIASDKROOT or MFX_HOME; not building msdk of OneVPL')
+  subdir_done()
+endif
+
+# Check oneVPL firstly
+mfx_ovpl_incdir = join_paths([mfx_ovpl_incdir, 'vpl'])
+mfx_ovpl_inc = include_directories(mfx_ovpl_incdir)
+
+msdk_ovpl_sources += [ '../gstmsdkvp9dec.c' ]
+msdk_ovpl_extra_args += ['-DUSE_MSDK_VP9_DEC=1']
+
+# Usually MFX_VERSION 1026+ is required to support raw VP9 stream, however Fedora 30 has MFX_VERSION==1026
+# but without support for raw VP9 stream, so mfxExtVP9Param is checked as well.
+# See https://gitlab.freedesktop.org/gstreamer/gst-plugins-bad/merge_requests/988#note_408093
+mfx_ver126_check_code = '''
+#include <mfxdefs.h>
+#include <mfxstructures.h>
+#if MFX_VERSION < 1026
+#error "The current version of mfx doesn't support raw vp9 stream"
+#endif
+mfxExtVP9Param ext;
+'''
+
+have_mfx_ver126 = cc.compiles(mfx_ver126_check_code,
+                include_directories : [configinc, mfx_ovpl_inc])
+
+if have_mfx_ver126
+  msdk_ovpl_sources += [ '../gstmsdkvp9enc.c' ]
+  msdk_ovpl_extra_args += ['-DUSE_MSDK_VP9_ENC=1']
+endif
+
+mfx_ver134_check_code = '''
+#include <mfxdefs.h>
+#if MFX_VERSION < 1034
+#error "The current version of mfx doesn't support AV1 decoding"
+#endif
+'''
+
+have_mfx_ver134 = cc.compiles(mfx_ver134_check_code,
+                include_directories : [configinc, mfx_ovpl_inc])
+
+if have_mfx_ver134
+  msdk_ovpl_sources += [ '../gstmsdkav1dec.c' ]
+  msdk_ovpl_extra_args += ['-DUSE_MSDK_AV1_DEC=1']
+endif
+
+libva_dep = dependency('libva-drm', required: get_option('msdk'))
+libdl_dep = cc.find_library('dl', required: get_option('msdk'))
+libgudev_dep = dependency('gudev-1.0', required: get_option('msdk'))
+libdrm_dep = dependency('libdrm', required: get_option('msdk'))
+msdk_ovpl_deps = declare_dependency(dependencies: [libva_dep, libdl_dep, libgudev_dep, libdrm_dep])
+msdk_ovpl_deps_found = libva_dep.found() and libdl_dep.found() and libgudev_dep.found() and libdrm_dep.found()
+
+msdk_ovpl_plugins_install_dir = plugins_install_dir + '-msdk-onevpl'
+gst_msdk_incdir = include_directories('..')
+
+if msdk_ovpl_deps_found
+  msdk_ovpl_extra_args += ['-DGST_USE_UNSTABLE_API']
+  gstmsdkovpltag = library('gstmsdk',
+    msdk_ovpl_sources,
+    c_args : gst_plugins_bad_args + msdk_ovpl_extra_args,
+    include_directories : [gst_msdk_incdir, configinc, mfx_ovpl_inc],
+    dependencies : [gstbase_dep, gstvideo_dep, gstpbutils_dep, gstallocators_dep, gstcodecparsers_dep, mfx_ovpl_dep, msdk_ovpl_deps],
+    install : true,
+    install_dir : msdk_ovpl_plugins_install_dir,
+  )
+  plugins += [gstmsdkovpltag]
+endif
-- 
2.24.3

