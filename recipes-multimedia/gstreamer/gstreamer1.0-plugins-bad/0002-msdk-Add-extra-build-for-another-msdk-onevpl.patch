From 5d073b2141e90f385247d87e4cddb8c6070d6d55 Mon Sep 17 00:00:00 2001
From: Lim Siew Hoon <siew.hoon.lim@intel.com>
Date: Mon, 14 Aug 2023 15:26:04 +0800
Subject: [PATCH 2/2] msdk: Add extra build for another msdk onevpl.

Signed-off-by: Lim Siew Hoon <siew.hoon.lim@intel.com>
---
 sys/msdk/onevpl/meson.build | 145 ++++++++++++++++++++++++++++++++++++
 1 file changed, 145 insertions(+)
 create mode 100644 sys/msdk/onevpl/meson.build

diff --git a/sys/msdk/onevpl/meson.build b/sys/msdk/onevpl/meson.build
new file mode 100644
index 000000000..fa92b7561
--- /dev/null
+++ b/sys/msdk/onevpl/meson.build
@@ -0,0 +1,145 @@
+msdk_ovpl_sources = [
+  '../gstmsdk.c',
+  '../gstmsdkallocator.c',
+  '../gstmsdkcaps.c',
+  '../gstmsdkcontext.c',
+  '../gstmsdkcontextutil.c',
+  '../gstmsdkdec.c',
+  '../gstmsdkdecproputil.c',
+  '../gstmsdkenc.c',
+  '../gstmsdkh264dec.c',
+  '../gstmsdkh264enc.c',
+  '../gstmsdkh265dec.c',
+  '../gstmsdkh265enc.c',
+  '../gstmsdkmjpegdec.c',
+  '../gstmsdkmjpegenc.c',
+  '../gstmsdkmpeg2dec.c',
+  '../gstmsdkmpeg2enc.c',
+  '../gstmsdkvc1dec.c',
+  '../gstmsdkvp8dec.c',
+  '../gstmsdkvpp.c',
+  '../gstmsdkvpputil.c',
+  '../msdk-enums.c',
+  '../msdk.c',
+]
+
+msdk_ovpl_extra_args = []
+
+extra_args = [
+  '-DGST_USE_UNSTABLE_API',
+]
+
+msdk_option = get_option('msdk')
+if msdk_option.disabled()
+  subdir_done()
+endif
+
+if host_machine.system() == 'windows'
+  msdk_ovpl_sources += ['../msdk_d3d.c', '../gstmsdkallocator_d3d.c' ]
+  if gstd3d11_dep.found()
+    extra_args += ['-DCOBJMACROS']
+  else
+    if msdk_option.enabled()
+      error('The msdk plugin was enabled explicity, but required d3d11 dependencies were not found.')
+    endif
+    subdir_done()
+  endif
+else
+  if not gstva_dep.found()
+    if msdk_option.enabled()
+      error('The msdk plugin was enabled explicity, but required va dependencies were not found.')
+    endif
+    subdir_done()
+  endif
+  msdk_ovpl_sources += ['../msdk_libva.c', '../gstmsdkallocator_libva.c']
+endif
+
+
+mfx_ovpl_dep = dependency('vpl', version: '>= 2.2', required: false)
+
+if mfx_ovpl_dep.found()
+  mfx_ovpl_incdir = mfx_ovpl_dep.get_variable('includedir')
+  mfx_ovpl_inc = []
+else
+  error('OneVPL SDK not found: consider setting PKG_CONFIG_PATH, INTELMEDIASDKROOT or MFX_HOME')
+  subdir_done()
+endif
+
+# Check oneVPL firstly
+mfx_ovpl_incdir = join_paths([mfx_ovpl_incdir, 'vpl'])
+mfx_ovpl_inc = include_directories(mfx_ovpl_incdir)
+
+msdk_ovpl_sources += [ '../gstmsdkvp9dec.c' ]
+msdk_ovpl_extra_args += ['-DUSE_MSDK_VP9_DEC=1']
+
+
+# Usually MFX_VERSION 1026+ is required to support raw VP9 stream, however Fedora 30 has MFX_VERSION==1026
+# but without support for raw VP9 stream, so mfxExtVP9Param is checked as well.
+# See https://gitlab.freedesktop.org/gstreamer/gst-plugins-bad/merge_requests/988#note_408093
+mfx_ver126_check_code = '''
+#include <mfxdefs.h>
+#include <mfxstructures.h>
+#if MFX_VERSION < 1026
+#error "The current version of mfx doesn't support raw vp9 stream"
+#endif
+mfxExtVP9Param ext;
+'''
+
+have_mfx_ver126 = cc.compiles(mfx_ver126_check_code,
+                include_directories : [configinc, mfx_ovpl_inc])
+
+if have_mfx_ver126
+  msdk_ovpl_sources += [ '../gstmsdkvp9enc.c' ]
+  msdk_ovpl_extra_args += ['-DUSE_MSDK_VP9_ENC=1']
+endif
+
+mfx_ver134_check_code = '''
+#include <mfxdefs.h>
+#if MFX_VERSION < 1034
+#error "The current version of mfx doesn't support AV1 decoding"
+#endif
+'''
+
+have_mfx_ver134 = cc.compiles(mfx_ver134_check_code,
+                include_directories : [configinc, mfx_ovpl_inc])
+
+if have_mfx_ver134
+  msdk_ovpl_sources += [ '../gstmsdkav1dec.c' ]
+  msdk_ovpl_extra_args += ['-DUSE_MSDK_AV1_DEC=1']
+endif
+
+mfx_ver205_check_code = '''
+#include <mfxdefs.h>
+#if MFX_VERSION < 2005
+#error "The current verison of mfx doesn't support AV1 encoding"
+#endif
+'''
+
+have_mfx_ver205 = cc.compiles(mfx_ver205_check_code,
+                include_directories: [configinc, mfx_ovpl_inc])
+
+if have_mfx_ver205
+  msdk_ovpl_sources += [ '../gstmsdkav1enc.c' ]
+  msdk_ovpl_extra_args += ['-DUSE_MSDK_AV1_ENC=1']
+endif
+
+libdl_dep = cc.find_library('dl', required: get_option('msdk'))
+libgudev_dep = dependency('gudev-1.0', required: get_option('msdk'), allow_fallback: true)
+libdrm_dep = dependency('libdrm', required: get_option('msdk'))
+msdk_ovpl_deps = declare_dependency(dependencies: [gstva_dep, libdl_dep, libgudev_dep, libdrm_dep])
+msdk_ovpl_deps_found = gstva_dep.found() and libdl_dep.found() and libgudev_dep.found() and libdrm_dep.found()
+
+msdk_ovpl_plugins_install_dir = plugins_install_dir + '-msdk-onevpl'
+gst_msdk_incdir = include_directories('..')
+
+if msdk_ovpl_deps_found
+  gstmsdkovpltag = library('gstmsdk',
+    msdk_ovpl_sources,
+    c_args : gst_plugins_bad_args + extra_args +  msdk_ovpl_extra_args,
+    include_directories : [gst_msdk_incdir, configinc, mfx_ovpl_inc],
+    dependencies : [gstbase_dep, gstvideo_dep, gstpbutils_dep, gstallocators_dep, gstcodecparsers_dep, mfx_ovpl_dep, msdk_ovpl_deps],
+    install : true,
+    install_dir : msdk_ovpl_plugins_install_dir,
+  )
+  plugins += [gstmsdkovpltag]
+endif
-- 
2.40.1

